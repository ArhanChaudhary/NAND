class _2048 {
    static int lcg;
    static int characterPixelWidth, characterPixelHeight;
    static int spaceX, spaceY;
    field int size;
    field int gridX, gridY;
    field int score;
    field int combinedPosition;
    field int takenCount;
    field int score;
    field boolean validDirection;
    field boolean exit;
    field Array grid;

    function void init() {
        let lcg = 1234;
        let characterPixelWidth = 8;
        let characterPixelHeight = 11;
        let spaceX = 6;
        let spaceY = 3;
        return;
    }

    constructor _2048 new(int size_) {
        var int i;
        var int j;
        var Array tmp;

        let score = 0;
        let gridX = 21;
        let gridY = 11;
        let size = size_;
        
        let takenCount = 0;
        let exit = false;
        let grid = Array.new(size);
        let i = 0;
        do initUI();
        while (i < size) {
            let tmp = Array.new(size);
            let grid[i] = tmp;
            let j = 0;
            while (j < size) {
                let tmp[j] = 0;
                let j = j + 1;
            }
            let i = i + 1;
        }
        return this;
    }

    method void initUI() {
        var int i;
        var int rawGridX;
        var int rawGridY;
        let rawGridX = (gridX - 1) * characterPixelWidth;
        let rawGridY = (gridY - 1) * characterPixelHeight;

        let i = 0;
        while (i < (size + 1)) {
            do Screen.drawRectangle(
                rawGridX,
                rawGridY + ((i * spaceY) * characterPixelHeight),
                rawGridX + ((size * spaceX) * characterPixelWidth),
                rawGridY + ((i * spaceY) * characterPixelHeight)
            );
            let i = i + 1;
        }
        let i = 0;
        while (i < (size + 1)) {
            do Screen.drawRectangle(
                rawGridX + ((i * spaceX) * characterPixelWidth),
                rawGridY,
                rawGridX + ((i * spaceX) * characterPixelWidth),
                rawGridY + ((size * spaceY) * characterPixelHeight)
            );
            let i = i + 1;
        }

        do Output.moveCursor(gridY - 3, gridX - 1);
        do Output.printString("Score:");
        return;
    }

    method void run() {
        var int key;

        do setRandomPosition();
        while (~exit) {
            while (key = 0) {
                let key = Keyboard.keyPressed();
            }
            let validDirection = false;
            if (key = 131) {
                do moveGrid(1); // up
            }
            if (key = 133) {
                do moveGrid(2); // down
            }
            if (key = 130) {
                do moveGrid(3); // left
            }
            if (key = 132) {
                do moveGrid(4); // right
            }
            if (validDirection) {
                do drawGrid();
                do setRandomPosition();
            }
            while (~(key = 0)) {
                let key = Keyboard.keyPressed();
            }
        }
        // you win/lose
        do Output.printString("You lose, try again");
        return;
    }

    method void moveGrid(int direction) {
        var int i;
        var int j;
        var Array tmp;

        var int iStep;
        var int iNext;
        var int iStop;

        if (direction = 1) {
            let i = 0;
            let iStep = size;
            let iNext = 1 - (size * size);
            let iStop = size;
        }
        if (direction = 2) {
            let i = size * (size - 1);
            let iStep = -size;
            let iNext = (size * size) + 1;
            let iStop = size * size;
        }
        if (direction = 3) {
            let i = 0;
            let iStep = 1;
            let iNext = 0;
            let iStop = size * size;
        }
        if (direction = 4) {
            let i = size - 1;
            let iStep = (-1);
            let iNext = size * 2;
            let iStop = (size * (size + 1)) - 1;
        }
        
        let j = 0;
        let combinedPosition = (-1);
        while (~(i = iStop)) {
            while (j < size) {
                do tryMove(i, direction, true);
                let i = i + iStep;
                let j = j + 1;
            }
            let j = 0;
            let i = i + iNext;
        }
        return;
    }

    method void tryMove(int position, int direction, boolean firstIteration) {
        var int positionValue;
        var int nextPosition;
        var int nextPositionValue;
        let positionValue = getPosition(position);
        if (positionValue = 0) {
            return;
        }
        if (direction = 1) {
            let nextPosition = position - size;
            if (position < size) {
                return;
            }
        }
        if (direction = 2) {
            let nextPosition = position + size;
            if (~(position < (size * (size - 1)))) {
                return;
            }
        }
        if (direction = 3) {
            let nextPosition = position - 1;
            if ((position - ((position / size) * size)) = 0) {
                return;
            }
        }
        if (direction = 4) {
            let nextPosition = position + 1;
            if ((position - ((position / size) * size)) = (size - 1)) {
                return;
            }
        }
        let nextPositionValue = getPosition(nextPosition);
        if (~(nextPositionValue = 0)) {
            if ((~(position = combinedPosition)) & (~(nextPosition = combinedPosition)) & (positionValue = nextPositionValue)) {
                let validDirection = true;
                let combinedPosition = nextPosition;
                do setPosition(nextPosition, positionValue * 2, false);
                do erasePosition(position, positionValue, firstIteration);
            }
            return;
        }
        let takenCount = takenCount + 1;
        let validDirection = true;
        do setPosition(nextPosition, positionValue, false);
        do erasePosition(position, positionValue, firstIteration);
        do tryMove(nextPosition, direction, false);
        return;
    }

    method void erasePosition(int position, int positionValue, boolean draw) {
        var int div;
        var int mod;
        var Array tmp;
        let div = position / size;
        let mod = position - (div * size);

        let tmp = grid[div];
        let tmp[mod] = 0;

        if (draw) {
            if (positionValue > 99) {
                do cursorTo(div, mod);
                do Output.printString("   ");
            } else {if (positionValue > 9) {
                do cursorTo(div, mod);
                do Output.printString("  ");
            }}
        }
        let takenCount = takenCount - 1;
        do setPosition(position, 0, false);
        return;
    }

    method void cursorTo(int row, int col) {
        do Output.moveCursor((row * spaceY) + gridY, (col * spaceX) + gridX);
        return;
    }

    method void drawGrid() {
        var int i;
        var int j;
        var Array tmp;
        var int tmp2;
        let i = 0;
        while (i < size) {
            let tmp = grid[i];
            let j = 0;
            while (j < size) {
                let tmp2 = tmp[j];
                do cursorTo(i, j);
                if (~(tmp2 = 0)) {
                    do Output.printInt(tmp2);
                }
                let j = j + 1;
            }
            do Output.println();
            do Output.println();
            let i = i + 1;
        }
        return;
    }

    method void setRandomPosition() {
        var int randomPositionValue;
        var int lcgSize;
        if (takenCount = (size * size)) {
            let exit = true;
            return;
        }
        let lcg = Math.abs((3 * lcg) + 1234);
        let lcg = lcg - ((lcg / 16385) * 16385);
        let lcgSize = lcg - ((lcg / (size * size)) * (size * size));
        let randomPositionValue = getPosition(lcgSize);
        if (randomPositionValue = 0) {
            let takenCount = takenCount + 1;
            if ((lcg - ((lcg / 10) * 10)) > 5) {
                do setPosition(lcgSize, 4, true);
                let score = score + 4;
            } else {
                do setPosition(lcgSize, 2, true);
                let score = score + 2;
            }
            do Output.moveCursor(gridY - 3, gridX + 6);
            do Output.printInt(score);
            return;
        }
        do setRandomPosition();
        return;
    }

    method int getPosition(int pos) {
        var int div;
        var int mod;
        var Array tmp;
        let div = pos / size;
        let mod = pos - (div * size);

        let tmp = grid[div];
        return tmp[mod];
    }

    method void setPosition(int pos, int value, boolean draw) {
        var int div;
        var int mod;
        var Array tmp;
        let div = pos / size;
        let mod = pos - (div * size);
        if (draw) {
            do cursorTo(div, mod);
            do Output.printInt(value);
        }

        let tmp = grid[div];
        let tmp[mod] = value;
        return;
    }

    method void dispose() {
        var int i;
        var Array tmp;
        let i = 0;
        while (i < size) {
            let tmp = grid[i];
            do tmp.dispose();
            let i = i + 1;
        }
        do Memory.deAlloc(this);
        return;
    }
}