class _2048 {
    static int gridX, gridY;
    static int size;
    static int lcg;
    field Array grid;

    function void init() {
        let gridX = 5;
        let gridY = 5;
        let size = 4;
        let lcg = 1234;
        return;
    }

    constructor _2048 new() {
        var int i;
        var int j;
        var Array tmp;
        let grid = Array.new(4);
        let i = 0;
        while (i < size) {
            let tmp = Array.new(4);
            let grid[i] = tmp;
            let j = 0;
            while (j < size) {
                let tmp[j] = 0;
                let j = j + 1;
            }
            let i = i + 1;
        }
        return this;
    }

    method void run() {
        var int key;
        var boolean exit;

        let exit = false;
        do setRandomPosition();
        do drawGrid();
        while (~exit) {
            while (key = 0) {
                let key = Keyboard.keyPressed();
            }
            if (key = 131) {
                do moveGrid(1); // up
            }
            if (key = 133) {
                do moveGrid(2); // down
            }
            if (key = 130) {
                do moveGrid(3); // left
            }
            if (key = 132) {
                do moveGrid(4); // right
            }
            do setRandomPosition();
            do drawGrid();
            while (~(key = 0)) {
                let key = Keyboard.keyPressed();
            }
        }
        return;
    }

    method void moveGrid(int direction) {
        var int i;
        var int j;
        var Array tmp;

        var int iStep;
        var int iNext;
        var int iStop;

        if (direction = 1) {
            let i = 0;
            let iStep = 4;
            let iNext = (-15);
            let iStop = 4;
        }
        if (direction = 2) {
            let i = 12;
            let iStep = (-4);
            let iNext = 17;
            let iStop = 16;
        }
        if (direction = 3) {
            let i = 0;
            let iStep = 1;
            let iNext = 0;
            let iStop = 16;
        }
        if (direction = 4) {
            let i = 3;
            let iStep = (-1);
            let iNext = 8;
            let iStop = 19;
        }
        
        let j = 0;
        while (~(i = iStop)) {
            while (j < 4) {
                do tryMove(i, direction);
                let i = i + iStep;
                let j = j + 1;
            }
            let j = 0;
            let i = i + iNext;
        }
        return;
    }

    method void tryMove(int position, int direction) {
        var int positionValue;
        var int nextPosition;
        var int nextPositionValue;
        let positionValue = getPosition(position);
        if (positionValue = 0) {
            return;
        }
        if (direction = 1) {
            let nextPosition = position - 4;
            if (
                (position = 0) |
                (position = 1) |
                (position = 2) |
                (position = 3)
            ) {
                return;
            }
        }
        if (direction = 2) {
            let nextPosition = position + 4;
            if (
                (position = 12) |
                (position = 13) |
                (position = 14) |
                (position = 15)
            ) {
                return;
            }
        }
        if (direction = 3) {
            let nextPosition = position - 1;
            if (
                (position = 0) |
                (position = 4) |
                (position = 8) |
                (position = 12)
            ) {
                return;
            }
        }
        if (direction = 4) {
            let nextPosition = position + 1;
            if (
                (position = 3) |
                (position = 7) |
                (position = 11) |
                (position = 15)
            ) {
                return;
            }
        }
        let nextPositionValue = getPosition(nextPosition);
        if (~(nextPositionValue = 0)) {
            if (positionValue = nextPositionValue) {
                do setPosition(nextPosition, positionValue * 2);
                do setPosition(position, 0);
                do drawGrid();
            }
            return;
        }
        do setPosition(nextPosition, positionValue);
        do setPosition(position, 0);
        do drawGrid();
        do tryMove(nextPosition, direction);
        return;
    }

    method void drawGrid() {
        var int i;
        var int j;
        var Array tmp;
        var int tmp2;
        do Output.moveCursor(0, 0);
        let i = 0;
        while (i < size) {
            let tmp = grid[i];
            let j = 0;
            while (j < size) {
                let tmp2 = tmp[j];
                if (tmp2 < 10) {
                    do Output.printInt(tmp[j]);
                    do Output.printString("   ");
                } else {
                    if (tmp2 < 100) {
                        do Output.printInt(tmp[j]);
                        do Output.printString("  ");
                    } else {
                        if (tmp2 < 1000) {
                            do Output.printInt(tmp[j]);
                            do Output.printString(" ");
                        }
                    }
                }
                let j = j + 1;
            }
            do Output.println();
            do Output.println();
            let i = i + 1;
        }
        return;
    }

    method void setRandomPosition() {
        var int randomPositionValue;
        var int lcg16;
        let lcg = Math.abs((3 * lcg) + 1234);
        let lcg = lcg - ((lcg / 16385) * 16385);
        let lcg16 = lcg - ((lcg / 16) * 16);
        let randomPositionValue = getPosition(lcg16);
        if (randomPositionValue = 0) {
            do setPosition(lcg16, 2);
            return;
        }
        do setRandomPosition();
        return;
    }

    method int getPosition(int pos) {
        var int div;
        var int mod;
        var Array tmp;
        let div = pos / 4;
        let mod = pos - (div * 4);

        let tmp = grid[div];
        return tmp[mod];
    }

    method void setPosition(int pos, int value) {
        var int div;
        var int mod;
        var Array tmp;
        let div = pos / 4;
        let mod = pos - (div * 4);

        let tmp = grid[div];
        let tmp[mod] = value;
        return;
    }

    method void dispose() {
        var int i;
        var Array tmp;
        let i = 0;
        while (i < size) {
            let tmp = grid[i];
            do tmp.dispose();
            let i = i + 1;
        }
        do Memory.deAlloc(this);
        return;
    }
}