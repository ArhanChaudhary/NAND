class Population {
    static Array dots;
    static Array newBrainDirections;
    static int gen;
    static int size;
    static Array fitnessCache;
    static int brainSize;

    constructor Population new(int _size, int _brainSize) {
        var int i;
        let brainSize = _brainSize;
        let gen = 1;
        let size = _size;
        let fitnessCache = Array.new(size);
        let dots = Array.new(size);
        let newBrainDirections = Array.new(size - 1);
        while (i < size) {
            let dots[i] = Dot.new(Brain.new());
            let i = i + 1;
        }

        // auxilliary memory
        let i = 0;
        while (i < (size - 1)) {
            let newBrainDirections[i] = Array.new(brainSize);
            let i = i + 1;
        }
        do Screen.clearScreen();
        return this;
    }

    function int getGen() {
        return gen;
    }

    method void update(boolean onlyBest) {
        var int i;
        var Dot dot;
        var Brain brain;
        if (~onlyBest) {
            while (i < size) {
                let dot = dots[i];
                let brain = dot.getBrain();
                do dot.update();
                do dot.show();
                let i = i + 1;
            }
        } else {
            while (i < size) {
                let dot = dots[i];
                let brain = dot.getBrain();
                do dot.update();
                let i = i + 1;
            }
            let dot = dots[0];
            do dot.show();
        }
    }

    method boolean allDotsDead() {
        var int i;
        var Dot dot;
        while (i < size) {
            let dot = dots[i];
            if (~dot.getDead())
                return false;
            let i = i + 1;
        }
        return true;
    }

    method void naturalSelection() {
        var Dot dot;
        var Dot bestDot;
        var Brain brain;
        var int dotFitness;
        var int bestFitness;
        var int i;
        var int j;
        var int selectionSum;
        var int selectionSumCoef;
        var int randFitness;
        var int randFitnessCoef;
        var int fitnessSum;
        var int fitnessSumCoef;
        var Array directions;
        var Array newDirections;
        var int scaleCache;

        let bestFitness = -1;
        while (i < size) {
            let dot = dots[i];
            let dotFitness = dot.calculateFitness();
            let fitnessCache[i] = dotFitness;
            if (dotFitness > bestFitness) {
                let bestFitness = dotFitness;
                let bestDot = dot;
            }
            let fitnessSum = fitnessSum + dotFitness;
            if (fitnessSum < 0) {
                let fitnessSum = fitnessSum + 32767 + 1;
                let fitnessSumCoef = fitnessSumCoef + 1;
            }
            let i = i + 1;
        }

        if (bestDot.getReachedGoal()) {
            let brain = bestDot.getBrain();
            do Dot.setMinStep(brain.getStep());
        }

        let i = 0;
        while (i < (size - 1)) {
            let randFitness = Math.abs(Util.random());
            let randFitnessCoef = 32767;
            if (~(fitnessSumCoef = 0)) {
                let scaleCache = 32767 / fitnessSumCoef;
                while (randFitnessCoef > fitnessSumCoef) {
                    // fitnessSumCoef = 296, randFitnessCoef = 32698
                    // this results in randFitnessCoef = 297 which is out of bounds
                    let randFitnessCoef = Math.abs(Util.random()) / scaleCache;
                }
            } else {
                let randFitnessCoef = 0;
            }
            if (randFitnessCoef = fitnessSumCoef) {
                let scaleCache = 32767 / fitnessSum;
                while (~(randFitness < fitnessSum)) {
                    // same with this it can also go out of bounds
                    let randFitness = Math.abs(Util.random()) / scaleCache;
                }
            }
            let selectionSum = 0;
            let selectionSumCoef = 0;
            let j = 0;
            while (j < size) {
                let selectionSum = selectionSum + fitnessCache[j];
                if (selectionSum < 0) {
                    let selectionSum = selectionSum + 32767 + 1;
                    let selectionSumCoef = selectionSumCoef + 1;
                }
                if ((selectionSumCoef > randFitnessCoef) | ((selectionSumCoef = randFitnessCoef) & (selectionSum > randFitness))) {
                    let dot = dots[j];
                    let j = size;
                }
                let j = j + 1;
            }
            let brain = dot.getBrain();
            let directions = brain.getDirections();
            let newDirections = newBrainDirections[i];
            let j = 0;
            while (j < brainSize) {
                if (~((Util.random() & 32256) = 0)) {
                    let newDirections[j] = directions[j];
                } else {
                    let newDirections[j] = AccelerationVector.random();
                }
                let j = j + 1;
            }
            let i = i + 1;
        }
        let i = 0;
        while (i < size) {
            let dot = dots[i];
            let brain = dot.getBrain();
            let directions = brain.getDirections();
            do dot.instantiate();
            do brain.instantiate();
            if (~(i = 0)) {
                let newDirections = newBrainDirections[i - 1];
            } else {
                let brain = bestDot.getBrain();
                let newDirections = brain.getDirections();
            }
            let j = 0;
            while (j < brainSize) {
                let directions[j] = newDirections[j];
                let j = j + 1;
            }
            let i = i + 1;
        }
        do Screen.clearScreen();
        let gen = gen + 1;
    }
}