class Dot {
    static int initialX, initialY;
    field int posX, posY;
    field int velX, velY;
    field AccelerationVector acc;
    static int goalX, goalY;
    field int prevX, prevY;
    field Brain brain;
    field boolean dead;
    field boolean reachedGoal;
    static int stepWeight;
    static int brainSize;
    static int minStep;
    static Array obstacles;

    function void config(int _initialX, int _initialY, int _goalX, int _goalY, int _brainSize, Array _obstacles) {
        let initialX = _initialX;
        let initialY = _initialY;
        let goalX = _goalX;
        let goalY = _goalY;
        let brainSize = _brainSize;
        let minStep = 32767;
        let stepWeight = (32767 - 10000) / _brainSize;
        let obstacles = _obstacles;
    }

    constructor Dot new() {
        let brain = Brain.new();
        do instantiate();
        return this;
    }

    method void instantiate() {
        let dead = false;
        let reachedGoal = false;
        let prevX = initialX;
        let prevY = initialY;

        let posX = initialX;
        let posY = initialY;
        let velX = 0;
        let velY = 0;
        let acc = null;
    }

    method boolean getDead() {
        return dead;
    }

    function int getMinStep() {
        return minStep;
    }

    function void setMinStep(int _minStep) {
        let minStep = _minStep;
    }

    method Brain getBrain() {
        return brain;
    }

    method void setBrain(Brain _brain) {
        let brain = _brain;
    }

    method boolean getReachedGoal() {
        return reachedGoal;
    }

    method void show() {
        if (~dead) {
            do Screen.setColor(false);
            do Screen.drawRectangle(prevX, prevY, prevX + 2, prevY + 2);
            do Screen.setColor(true);
            let prevX = posX - 1;
            let prevY = posY - 1;
            do Screen.drawRectangle(prevX, prevY, prevX + 2, prevY + 2);
        }
    }

    method void update() {
        var boolean tmp, tmp2;
        if (~(brain.getStep() > minStep)) {
            if (~dead) {
                if (~(brainSize > brain.getStep())) {
                    let dead = true;
                } else {
                    let acc = brain.getNextDirection();
                }

                let velX = velX + acc.getX();
                let velY = velY + acc.getY();
                let tmp = velX < 0;
                let tmp2 = velY < 0;

                if (tmp) {
                    let velX = -velX;
                }

                if (tmp2) {
                    let velY = -velY;
                }

                if (~(velX > 5)) {
                    if (~(velX = 4)) {
                        if (~(velX = 0)) {
                            if (~(velY < 5)) {
                                let velY = 4;
                            }
                        } else if (~(velY < 6)) {
                            let velY = 5;
                        }
                    } else if (~(velY < 4)) {
                        let velY = 3;
                    }
                } else {
                    let velX = 5;
                    let velY = 0;
                }

                if (tmp) {
                    let velX = -velX;
                }
                if (tmp2) {
                    let velY = -velY;
                }

                let posX = posX + velX;
                let posY = posY + velY;

                let tmp = 0;
                let tmp2 = posY;
                while (~(tmp2 < 16)) {
                    let tmp2 = tmp2 - 16;
                    let tmp = tmp + 32;
                }

                let tmp2 = posX;
                while (~(tmp2 < 16)) {
                    let tmp2 = tmp2 - 16;
                    let tmp = tmp + 1;
                }

                if (~((posX < 2) | (posY < 2) | (posX > 510) | (posY > 254) | obstacles[tmp])) {
                    if (~((Math.abs(posX - goalX) > 3) | (Math.abs(posY - goalY) > 3))) {
                        let reachedGoal = true;
                        let dead = true;
                    }
                } else {
                    let dead = true;
                }
            }
        } else {
            let dead = true;
        }
    }

    method int calculateFitness() {
        var int x;
        var int y;
        if (~reachedGoal) {
            let x = Math.abs(posX - goalX) / 4;
            let y = Math.abs(posY - goalY) / 4;
            return 32767 / Math.max(10, (x * x) + (y * y));
        }
        return Math.max(10000, 32767 - (stepWeight * brain.getStep()));
    }

    method void dispose() {
        do brain.dispose();
        do Memory.deAlloc(this);
    }
}