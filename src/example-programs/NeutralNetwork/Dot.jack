class Dot {
    field int posX, posY;
    field Vector vel;
    field Vector acc;
    static Vector goal;
    field Brain brain;
    field boolean dead;
    field boolean reachedGoal;
    static int stepWeight;
    field int prevX;
    field int prevY;

    function void init(Vector _goal) {
        let goal = _goal;
        let stepWeight = (32767 - 10000) / Brain.getBrainSize();
    }

    constructor Dot new(Brain _brain) {
        let dead = false;
        let reachedGoal = false;
        let prevX = 0;
        let prevY = 0;

        let brain = _brain;
        let posX = 10;
        let posY = 128;
        let vel = Vector.new(0, 0);
        let acc = null;
        return this;
    }

    method boolean getDead() {
        return dead;
    }

    method void setDead(boolean _dead) {
        let dead = _dead;
    }

    method Brain getBrain() {
        return brain;
    }

    method void setBrain(Brain _brain) {
        let brain = _brain;
    }

    method boolean getReachedGoal() {
        return reachedGoal;
    }

    method void show() {
        if (~dead) {
            do Screen.setColor(false);
            do Screen.drawRectangle(prevX, prevY, prevX + 2, prevY + 2);
            do Screen.setColor(true);
            let prevX = posX - 1;
            let prevY = posY - 1;
            do Screen.drawRectangle(prevX, prevY, prevX + 2, prevY + 2);
        }
    }

    method void update() {
        if (~dead) {
            if (~(Brain.getBrainSize() > brain.getStep())) {
                let dead = true;
            } else {
                let acc = brain.getNextDirection();
            }
            do vel.addVelocity(acc);
            let posX = posX + vel.getX();
            let posY = posY + vel.getY();

            if (~((posX < 2) | (posY < 2) | (posX > 510) | (posY > 254))) {
                if (~((Math.abs(posX - goal.getX()) > 3) | (Math.abs(posY - goal.getY()) > 3))) {
                    let reachedGoal = true;
                    let dead = true;
                }
            } else {
                let dead = true;
            }
        }
    }

    method int calculateFitness() {
        var int x;
        var int y;
        if (~reachedGoal) {
            let x = Math.abs(posX - goal.getX()) / 2;
            let y = Math.abs(posY - goal.getY()) / 2;
            return 32767 / Math.max(10, ((x * x) + (y * y)) - 100);
        }
        return Math.max(10000, 32767 - (stepWeight * brain.getStep()));
    }

    method Dot getBaby() {
        return Dot.new(brain.clone());
    }
}